eval 'exec perl -S $0 ${1+"$@"}'
  if $running_under_some_shell; $running_under_some_shell=0; # vim600:sw=2:tw=0:fdm=marker

# @(#)$Info: systemc_version - extracts version information for SystemC $
#
# @(#)$Copyright: 2003-2005 David C Black All rights reserved. $
# @(#)$License: GNU GPL. $
# @(#)$Email: <dcblack@ESLX.com>. $
# @(#)$Id$

require 5.006; # minimum Perl version
use strict;
use English;
use FindBin qw($RealBin $RealScript);
use vars qw($exe);

use Carp;
use FileHandle;
use Cwd;
STDOUT->autoflush(1);
STDERR->autoflush(1);

## BEGIN DOCUMENTATION #####################################################{{{
###############################################################################
$exe = "$RealBin/$RealScript";
exec "perldoc $exe" if "@ARGV" =~ m/^-+h(elp)?$/;
=pod

=head1 NAME

B<systemc_version> - extracts & displays the version of systemc

=head1 SYNOPSIS

B<systemc_version> [I<OPTIONS>]

=head1 DESCRIPTION

B<systemc_version> examines the SYSTEMC (or SYSTEMC_HOME) environment
variable and then extracts the SystemC version number from a file within
the SystemC include file hierarchy. The result is to print the version
number in one of several formats.

=head1 OPTIONS

 -help  this text
 -us    use underscore
 -full  full version (all 3 parts)
 -D     formated for gcc
 -int   integer format major*1e6+minor*1e3+bug
 -min # require a minimum version #
 -sq    squeeze out non-alpha
 -q     quiet, return appropriate exit status

=head1 EXAMPLES

 # Ensure version minimum
 systemc_version -min 2.1

 # Compile code ifdef'd on SystemC version
 gcc -D`systemc_version -D` top.cpp

=head1 AUTHOR

David C. Black <dcblack@ESLX.com>

=head1 COPYRIGHT

Copyright (C) 2003-2007 David C Black

=head1 LICENSE

This software, B<systemc_version>, is a copyrighted work. The GNU
GPL version 2 describes the conditions under which the software may
be copied, modified and distributed. No fee may be charged for this
software or any derivative work (whether in exchange for copying,
distribution, use, or otherwise).

=cut

##}}}

package main;

sub Die {
  printf "FATAL: %s\n",$_[0];
  exit 1;
};

my %OPT;

# Parse command-line
my $arg;
my $min = "2.0.1";
my $scale=1000;
while (@ARGV) {
  $arg = shift @ARGV;
  if ($arg eq '_' or $arg eq '-us') {
    $OPT{-us} = 1;
  } elsif ($arg eq '-full') {
    $OPT{-full} = 1;
  } elsif ($arg eq '-var' or $arg eq '-D') {
    $OPT{-var} = 1;
  } elsif ($arg eq '-int') {
    $OPT{-int} = 1;
  } elsif ($arg eq '-min') {
    $min = shift @ARGV;
    Die "Must be a dotted or underscore separated number!" unless $min =~ m/[1-9]\d*([._]\d+){,2}/;
    $min =~ s/\./_/g;
  } elsif ($arg eq '-sq') {
    $OPT{-sq} = 1;
  } elsif ($arg eq '-q') {
    $OPT{-q} = 1;
  } elsif ($arg eq '-test') {
    $OPT{-test} = shift @ARGV;
  } else {
  }#endif
}#endwhile

# Find location of SystemC
my $SYSTEMC;
$SYSTEMC = $ENV{'SYSTEMC'};
$SYSTEMC = $ENV{'SYSTEMC_HOME'} if $SYSTEMC eq '';
&Die("SYSTEMC environment variable is undefined") if $SYSTEMC eq '';

my $api_version_file = $SYSTEMC.'/include/systemc/kernel/sc_ver.h';
$api_version_file = $SYSTEMC.'/include/sysc/kernel/sc_ver.h' unless -r $api_version_file;

open(API_VERSION,"<$api_version_file") 
  or &Die("Unable to read $api_version_file!!?");

# Scan for definition of API version
my $api_version = $min;
while (<API_VERSION>) {
  next unless m/#define\s+SC_API_VERSION_STRING\s+sc_api_version_(\w+)/;
  $api_version = $1;
  last;
}#endwhile <API_VERSION>
close(API_VERSION);
$api_version = $OPT{-test} if defined $OPT{-test};

$api_version =~ s/\./_/g;

my $exit = ($api_version ge $min)?0:1;

# Massage format of variable as directed by command-line options
# Remove third rank
$api_version =~ s/(\d+_\d+)_\d+$/$1/ if not defined $OPT{-full};
# Integer format
if (defined $OPT{-int}) {
  $api_version =~ m/(\d+)_(\d+)(?:_(\d+))?/;
  my ($major,$minor,$bug) = ($1,$2,$3);
  $bug = 0 unless defined $bug;
  $api_version = ($major*$scale+$minor)*$scale+$bug;
}#endif
# Remove all underscores (squeeze)
$api_version =~ s/_//g if defined $OPT{-sq};
# Create a variable name suitable for use in make or environment
$api_version =~ s/^/SYSTEMC_V/ if defined $OPT{-var};
# Change underscores to decimals
$api_version =~ s/_/./g unless defined $OPT{-var} or defined $OPT{-us};

# Announce results and exit successfully
exit $exit if defined $OPT{-q};
printf "%s\n",$api_version;
exit $exit;
